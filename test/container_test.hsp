#include "../abdata/container.as"

#ifndef true
 #define global true 1
#endif

#module
#defcfunc make_iota_container int n,  local self
	self = container_make()
	repeat n
		container_push_back self, cnt
	loop
	return self
#global

	randomize

	//\’zƒeƒXƒg
	
	int_var = 0
	double_var = M_PI
	str_var = "container_test"
	
	container_new con0
	container_new con1, 1, double_var
	container_new con2, 7, str_var
	
	assert container_size(con0) == 0
	assert container_size(con1) == 1
	assert container_get(con1, cnt) == double_var
	assert container_size(con2) == 7
	repeat container_size(con2)
		assert container_get(con2, cnt) == str_var
	loop
	
	//—v‘f’Ç‰Á
	
	//push_back
	container_new con, 3, str_var
	container_push_back con, M_PI
	assert container_size(con) == 4
	assert container_get(con, 3) == M_PI
	repeat 3
		assert container_get(con, cnt) == str_var
	loop
	container_delete con
	
	//push_front
	con = make_iota_container(4)
	container_push_front con, M_PI
	assert container_size(con) == 5
	assert container_get(con, 0) == M_PI
	repeat 4, 1
		assert container_get(con, cnt) == cnt - 1
	loop
	
	//insert mid
	con = make_iota_container(5)
	container_insert con, M_PI, 2
	assert container_size(con) == 6
	repeat 2
		assert container_get(con, cnt) == cnt
	loop
	assert container_get(con, 2) == M_PI
	repeat 3, 3
		assert container_get(con, cnt) == cnt - 1
	loop
	
	//insert after last
	con = make_iota_container(3)
	container_insert con, 7, 7
	assert container_size(con) == 8
	repeat 8
		if ( cnt < 3 || cnt == 7 ) {
			assert container_get(con, cnt) == cnt
		} else {
			assert container_get(con, cnt) == stt_zero@
		}
	loop
	
	//pushvŒn: –¢À‘•
	//—v‘f‚Ì”{‰Á: –¢À‘•
	
	//ƒTƒCƒYw’è
	
	//to extend
	con = make_iota_container(3)
	container_resize con, 5, str_var
	assert container_size(con) == 5
	repeat 5
		if ( cnt < 3 ) {
			assert container_get(con, cnt) == cnt
		} else {
			assert container_get(con, cnt) == str_var
		}
	loop
	
	//to truncate
	con = make_iota_container(5)
	container_resize con, 2
	assert container_size(con) == 2
	repeat 2
		assert container_get(con, cnt) == cnt
	loop
	
	//œ‹
	con = make_iota_container(4)
	container_erase con, 2
	assert container_size(con) == 4 - 1
	
	expected = 0, 1, 3
	repeat 3
		assert container_get(con, cnt) == expected(cnt)
	loop
	
	//QÆ‰»
	con = make_iota_container(4)
	container_clone con, r, 2
	assert r == 2
	r = 99
	assert container_get(con, 2) == 99
	
	con = make_iota_container(3)
	container_ref(con, 1) ++
	assert container_get(con, 1) == 2
	
	//‘‚«Š·‚¦
	con = make_iota_container(4)
	container_set con, str_var, 2
	assert container_size(con) == 4
	assert container_get(con, 0) == 0
	assert container_get(con, 1) == 1
	assert container_get(con, 2) == str_var
	assert container_get(con, 3) == 3
	
	//ˆÚ“®AŒğŠ·A„‰ñA”½“]
	
	//Á‹
	con = make_iota_container(3)
	container_clear con
	assert container_size(con) == 0
	
	//˜AŒ‹
	con0 = make_iota_container(3)
	con1 = make_iota_container(4)
	container_chain con0, con1
	assert container_size(con0) == 3+4
	repeat 3
		assert container_get(con0, cnt) == cnt
	loop
	repeat 4
		assert container_get(con0, 3 + cnt) == container_get(con1, cnt)
	loop
	
	//ŒğŠ·
	con0 = make_iota_container(3)
	con1 = make_iota_container(4)
	container_swap con0, con1
	assert container_size(con0) == 4
	assert container_size(con1) == 3
	
	//®—ñ
	//’·‚³0
	con0 = make_iota_container(0)
	container_sort con0
	assert container_size(con0) == 0
	
	//—”
	con1 = container_make(5)
	repeat container_size(con1)
		container_set con1, rnd(1000)
	loop
	container_sort con1
	assert container_size(con1) == 5
	repeat container_size(con1) - 1
		assert container_get(con1, cnt) <= container_get(con1, cnt + 1)
	loop
	
	//•¡”‚ÌŒ^
	con2 = container_make()
	container_push_back con2, "b"
	container_push_back con2, 5
	container_push_back con2, 1
	container_push_back con2, "a"
	container_sort con2
	assert container_get(con2, 0) == "a"
	assert container_get(con2, 1) == "b"
	assert container_get(con2, 2) == 1
	assert container_get(con2, 3) == 5
	
	//“Yš”ÍˆÍƒ`ƒFƒbƒN
	con = make_iota_container(3)
	repeat 9, -3
		assert container_is_valid(con, cnt) == (0 <= cnt && cnt < 3)
	loop
	
	//”‚¦ã‚°
	con = container_make()
	container_push_back con, "1"
	container_push_back con, "a"
	container_push_back con, 1
	container_push_back con, 4
	one_int = 1
	assert container_count(con, one_int) == 2
	assert container_count(con, stt_zero) == 0
	
	//«‘‡”äŠr
	con1 = make_iota_container(4)
	con2 = make_iota_container(4)
	con3 = make_iota_container(5)
	container_set con2, ".", 1
	assert container_lexicographical_compare(con1, con1) == 0
	assert container_lexicographical_compare(con1, con2) > 0
	assert container_lexicographical_compare(con2, con3) < 0
	assert container_lexicographical_compare(con3, con2) > 0
	
	//•¡Ê
	con1 = make_iota_container(3)
	con2 = container_make()
	container_copy con2, con1
	assert container_lexicographical_compare(con2, con1) == 0
	
	
	//®—ñÏ‚İ‚©
	con = make_iota_container(3)
	assert container_is_sorted(con /*, SortMode_Ascending*/)
	
	container_reverse con
	assert container_is_sorted(con, SortMode_Decending)
	
	container_set con, 99, 2
	assert container_is_sorted(con, SortMode_Ascending) == 0
	assert container_is_sorted(con, SortMode_Decending) == 0
	
#module
#deffunc containerTest_eq_ran var self, int _value, int sort_mode,  int x_lb, int x_ub, \
	local value, local eq_ran
	
	value = _value
	container_equal_range self, value, eq_ran(0), eq_ran(1), sort_mode
	assert eq_ran(0) == x_lb
	assert eq_ran(1) == x_ub
	return
	
#deffunc containerTest_sorted_insert var _self, int _value, int can_dup, int sort_mode, \
	int x_lb, int x_ub, \
	local self, local value, local lb, local ub
	
	self = container_make()
	container_copy self, _self
	value = _value
	container_sorted_insertv self, value, can_dup, sort_mode
	container_equal_range    self, value, lb, ub,  sort_mode
	assert lb == x_lb
	assert ub == x_ub
	return
	
#deffunc containerTest_sorted_erase var _self, int _value, int max_count, int sort_mode,  int x_lb, int x_ub, \
	local self, local value, local lb, local ub
	
	self = container_make()
	container_copy self, _self
	value = _value
	container_sorted_erasev self, value, max_count, sort_mode
	container_equal_range   self, value, lb, ub,    sort_mode
	assert lb == x_lb
	assert ub == x_ub
	return
#global

	//‰ºŠEAãŠE
	con1 = container_make()
	expected = 1, 3, 3, 5, 5, 5, 7, 8
	foreach expected
		container_pushv_back con1, expected(cnt)
	loop
	
	//ŒŸõ
	containerTest_eq_ran con1, 0,,   0, 0
	containerTest_eq_ran con1, 2,,   1, 1
	containerTest_eq_ran con1, 3,,   1, 3
	containerTest_eq_ran con1, 8,,   7, 8
	containerTest_eq_ran con1, 9,,   8, 8
	
	//®—ñÏ‚İ‘}“ü
	//‘}“üŒã‚ÉA‘}“ü‚µ‚½’l‚Ì equal_range ‚ª‚Ç‚¤‚È‚é‚©
	containerTest_sorted_insert con1, 0,  true,,    0, 1
	containerTest_sorted_insert con1, 1,  true,,    0, 2
	containerTest_sorted_insert con1, 1, false,,    0, 1
	containerTest_sorted_insert con1, 2,  true,,    1, 2
	containerTest_sorted_insert con1, 3,  true,,    1, 4
	containerTest_sorted_insert con1, 3, false,,    1, 3
	containerTest_sorted_insert con1, 9,  true,,    8, 9
	containerTest_sorted_insert con1, 9, false,,    8, 9
	
	//®—ñÏ‚İ‚Ìœ‹
	//œ‹Œã‚ÉAœ‹‚³‚ê‚½’l‚Ì equal_range ‚ª‚Ç‚¤‚È‚é‚©
	containerTest_sorted_erase con1, 0,  1,,    0, 0
	containerTest_sorted_erase con1, 1,  1,,    0, 0
	containerTest_sorted_erase con1, 3,  1,,    1, 2
	containerTest_sorted_erase con1, 3, 99,,    1, 1
	containerTest_sorted_erase con1, 9, 99,,    8, 8
	
	con2 = container_make()
	container_chain   con2, con1
	container_reverse con2 //8, 7, 5, 5, 5, 3, 3, 1
	
	containerTest_eq_ran con2, 9, SortMode_Decending,   0, 0
	containerTest_eq_ran con2, 8, SortMode_Decending,   0, 1
	containerTest_eq_ran con2, 5, SortMode_Decending,   2, 5
	containerTest_eq_ran con2, 0, SortMode_Decending,   8, 8
	
	//®—ñÏ‚İ‘}“ü
	//‘}“üŒã‚ÉA‘}“ü‚µ‚½’l‚Ì equal_range ‚ª‚Ç‚¤‚È‚é‚©
	containerTest_sorted_insert con2, 9,  true, SortMode_Decending,    0, 1
	containerTest_sorted_insert con2, 8,  true, SortMode_Decending,    0, 2
	containerTest_sorted_insert con2, 7, false, SortMode_Decending,    1, 2
	containerTest_sorted_insert con2, 5,  true, SortMode_Decending,    2, 6
	containerTest_sorted_insert con2, 0,  true, SortMode_Decending,    8, 9
	
	//®—ñÏ‚İ‚Ìœ‹
	//œ‹Œã‚ÉAœ‹‚³‚ê‚½’l‚Ì equal_range ‚ª‚Ç‚¤‚È‚é‚©
	containerTest_sorted_erase con2, 9,  1, SortMode_Decending,    0, 0
	containerTest_sorted_erase con2, 5,  1, SortMode_Decending,    2, 4
	containerTest_sorted_erase con2, 3, 99, SortMode_Decending,    5, 5
	containerTest_sorted_erase con2, 0, 99, SortMode_Decending,    8, 8
	
	logmes "container test completed."
